apply plugin: android.support.SupportAndroidLibraryPlugin

dependencies {
    api project(':support-annotations')
    api project(':support-compat')

    androidTestImplementation libs.test_runner,      { exclude module: 'support-annotations' }
    androidTestImplementation libs.espresso_core,    { exclude module: 'support-annotations' }
    androidTestImplementation libs.mockito_core,     { exclude group: 'net.bytebuddy' } // DexMaker has it"s own MockMaker
    androidTestImplementation libs.dexmaker_mockito, { exclude group: 'net.bytebuddy' } // DexMaker has it"s own MockMaker
    androidTestImplementation project(':support-testutils')
}

android {
    defaultConfig {
        minSdkVersion 14
    }

    sourceSets {
        main.java.srcDirs = [
                'jellybean-mr2',
                'kitkat',
                'api21',
                'api22',
                'api23',
                'api24',
                'api26',
                'java'
        ]
        main.aidl.srcDirs = ['java']
        main.res.srcDirs 'res', 'res-public'
    }

    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

supportLibrary {
    name 'Android Support Library media compat'
    publish true
    inceptionYear '2011'
    description "The Support Library is a static library that you can add to your Android application in order to use APIs that are either not available for older platform versions or utility APIs that aren't a part of the framework APIs. Compatible on devices running API 14 or later."
}
